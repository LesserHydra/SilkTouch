plugins {
  id 'org.ajoberstar.grgit' version '1.4.2'
}

apply plugin: 'java'
apply plugin: 'eclipse'

import org.ajoberstar.grgit.*
import org.apache.tools.ant.filters.ReplaceTokens

version = generateVersion('1.1')
String pluginMain = 'com.lesserhydra.silktouch.SilkTouch'

repositories {
  mavenLocal()
  mavenCentral()
  maven {
    url "https://hub.spigotmc.org/nexus/content/repositories/snapshots/"
  }
}

dependencies {
  compile 'org.bukkit:bukkit:1.12.2-R0.1-SNAPSHOT'
  compile ('com.lesserhydra:HydraCore:1.0.0-SNAPSHOT') { transitive = false }
}

//Generate plugin.yml
processResources {
  inputs.file 'build.gradle' //So it's regenerated if version changes
  from 'templates/plugin.yml'
  filter(ReplaceTokens, tokens: [
          version:		version,
          mainClass:  pluginMain,
  ])
}

static String generateVersion(workingVersion) {
  Grgit repo = Grgit.open()
  Tag currentTag = repo.tag.list().find {it.commit == repo.head()}

  if (currentTag == null) return workingVersion + '-SNAPSHOT'
  return currentTag.getName()
}
